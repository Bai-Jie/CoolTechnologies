// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.1.0'
        classpath 'com.jakewharton.sdkmanager:gradle-plugin:0.12.0'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    group = PROJECTS_GROUP
    version = new Version(VERSION_NAME)
    status = version.status

    repositories {
        maven { url 'http://dl.bintray.com/optimalorange/maven' }
        mavenCentral()
        jcenter()
    }
}



import java.text.SimpleDateFormat
class Version {
    String originalVersion
    String versionName
    int versionCode
    String status
    Date buildTime
    Version(String versionValue) {
        if (versionValue =~ /\d{2,}/) {
            throw new GradleException("versionValue(${versionValue}) contains number more than 9.")
        }
        buildTime = new Date()
        originalVersion = versionValue
        String originalVersionCodeString;
        if (originalVersion.endsWith('-SNAPSHOT')) {
            status = 'integration'
            originalVersionCodeString = originalVersion.substring(0, originalVersion.length() - '-SNAPSHOT'.length())
            versionName = originalVersionCodeString + '-' + getTimestamp()
        } else {
            status = 'release'
            versionName = versionValue
            originalVersionCodeString = versionValue
        }
        versionCode = originalVersionCodeString.replace('.', '').toInteger();
    }
    String getTimestamp() {
        // Convert local file timestamp to UTC
        def format = new SimpleDateFormat('yyyyMMddHHmmss')
        format.setCalendar(Calendar.getInstance(TimeZone.getTimeZone('UTC')));
        return format.format(buildTime)
    }
    String toString() {
        versionName
    }
}
